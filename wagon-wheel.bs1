' {$STAMP BS1}
' {$PBASIC 1.0}

SYMBOL servo = PIN0
SYMBOL signal = W0
SYMBOL i = B11

SYMBOL slowest = 155
SYMBOL fastest = 170

' W6 = B12/13 used for subroutine calls

' PULSOUT sends a signal in units of 10us
'
' Modified Servo:
'   * 1.5ms (150 units) == stop
'   * bigger means speed left
'   * smaller means speed right
'   * on my one servo 152 was actually stop
'
' Unmodified Servo:
'   * 1.5ms (150 units) == center position
'   * 2.5ms (250 units) == 90 degrees left
'   * 0.5ms ( 50 units) == 90 degrees right
'   ? not sending any signal causes it to do nothing?
'   ? servo seems to not be exactly centered

signal = slowest

speedup:

FOR i = 1 TO 255 
    GOSUB servo_cmd
NEXT

signal = signal + 1
IF signal <= fastest THEN speedup

slowdown:

FOR i = 1 TO 255
    GOSUB servo_cmd
NEXT

signal = signal - 1
IF signal >= slowest THEN slowdown
GOTO speedup

servo_cmd:
  PULSOUT servo, signal
  PAUSE 20
  RETURN